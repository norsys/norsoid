buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "io.realm:realm-gradle-plugin:$REALM_VERSION"
        classpath "me.tatarka:gradle-retrolambda:$RETROLAMBDA_VERSION"
        classpath "io.fabric.tools:gradle:$FABRIC_TOOLS_VERSION"
    }
}


apply plugin: 'groovy'
apply plugin: 'maven'

version = NORSOID_VERSION
group = NORSOID_GROUP

uploadArchives {
    repositories {
        // TODO :  uniquement pour les tests en local
        mavenDeployer { repository(url: "file://${System.properties['user.home']}/.m2/repository") }
        /*mavenDeployer {
            repository(url: NORSYS_REPOSITORY_URL) {
                authentication(userName: nexusNorsysUser, password: nexusNorsysPassword)
            }
        }*/
    }
}


ext.norsoidProps = new Properties()
ext.norsoidProps.load(new FileInputStream(file("${project.rootDir}/norsoid.properties")))


sourceSets {
    main {
        groovy {
            srcDirs 'src'
            srcDirs 'build/gen'
        }
    }
}

task generateNorsoidConstants(type: Copy) {
    from 'src/templates'
    into "${project.buildDir}/gen/main/groovy/fr/norsys/android/norsoid"
    include 'NorsoidConstants.groovy.template'
    rename { file -> 'NorsoidConstants.groovy' }
    expand(project: project, title: 'Norsoid Constants', generated: new Date())
}
assemble.dependsOn generateNorsoidConstants

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "com.android.tools.build:gradle:$GRADLE_ANDROID_VERSION"
    compile "io.realm:realm-gradle-plugin:$REALM_VERSION"
    compile "me.tatarka:gradle-retrolambda:$RETROLAMBDA_VERSION"
    compile "io.fabric.tools:gradle:$FABRIC_TOOLS_VERSION"
}

// Make sure all code is compiled, tested and checked before uploadArchives.
uploadArchives.dependsOn build